class Ship : Actor {
  bool falling;
  Vector2 movement;
  float throttle;
  float throttleDelta;
  int forwardAngle;
  bool movingLeft;
  bool movingRight;
  bool throttleIncreasing;
  bool throttleDecreasing;

  Default {
    +NOGRAVITY;
  }

  States {
    Spawn:
      Goto Ready;
      stop;
    Ready:
      SKYA A 3;
      SKYA B 3;
      SKYA C 3;
      SKYA D 3;
      loop;
  }

  override void PostBeginPlay() {
    movement = (0,0);
    throttle = 0.0;
    throttleDelta = 0.0;
    falling = true;
    forwardAngle = angle;
    movingLeft = false;
    movingRight = false;
    throttleIncreasing = false;
    throttleDecreasing = false;
  }

  void mLeft(bool a) {
    movingLeft = a;
    setMovement();
  }
  void mRight(bool a) {
    movingRight = a;
    setMovement();
  }
  void throttleInc(bool a) {
    throttleIncreasing = a;
    setThrottle();
  }
  void throttleDec(bool a) {
    throttleDecreasing = a;
    setThrottle();
  }

  void setMovement() {
    float xspd = 0;
    if (movingLeft) {
      xspd -= 3;
    }
    if (movingRight) {
      xspd += 3;
    }
    movement = (xspd, movement.y);
  }

  void setThrottle() {
    float throttle = 0;
    if (throttleIncreasing) {
      throttle += 3;
    }
    if (throttleDecreasing) {
      throttle -= 3;
    }
    throttleDelta = throttle;
  }

  void jump() {
    if (falling == false) {
      movement = (movement.x, -15.0);
      falling = true;
    }
  }

  override void Tick() {
    Super.Tick();

    throttle = throttle + throttleDelta;
    if (throttle > 100) {
      throttle = 100;
    }
    if (throttle < 0) {
      throttle = 0;
    }

    // Console.Printf("Throttle: %f, ThrottleDelta: %f", throttle, throttleDelta);

    if (falling) {
      movement = (movement.x, movement.y + 0.8);
    }

    bool hitFront = pos.z - self.getZAt(20, 0) < -10;
    bool hitLeft = pos.z - self.getZAt(0, -20) < -10;
    bool hitRight = pos.z - self.getZAt(0, 20) < -10;

    float hitz = self.GetZAt(0, 0);
    hitz = max(hitz, self.GetZAt(-20, 0));
    hitz = max(hitz, self.GetZAt(20, 0));
    hitz = max(hitz, self.GetZAt(0, -20));
    hitz = max(hitz, self.GetZAt(0, 20));

    float zoffset = pos.z - hitz; //pos.z - RemoteRay.HitLocation.z;

    if (hitFront) {
      throttle = 0;
    }

    if (hitLeft) {
      movement = (max(0, movement.x), 0);
    }

    if (hitRight) {
      movement = (min(0, movement.x), 0);
    }

    if (zoffset > -10 && zoffset < movement.y) {
      movement = (movement.x, 0);
      SetOrigin((pos.x, pos.y, hitz), true);
      falling = false;
    } else if (zoffset > 0.1) {
      falling = true;
    }

    SetOrigin((pos.x + (Sin(forwardAngle) * movement.x) + (Cos(forwardAngle) * throttle / 20), pos.y - (Cos(forwardAngle) * movement.x) + (Sin(forwardAngle) * throttle / 20), pos.z - movement.y), true);

    PlayerInfo p = players[consoleplayer];
    p.mo.SetOrigin((pos.x - (Cos(forwardAngle) * 200), pos.y - (Sin(forwardAngle) * 200), 0), true);
  }
}