class Ship : Actor {
  Vector2 movement;
  float throttle;
  float throttleDelta;
  int forwardAngle;
  bool movingLeft;
  bool movingRight;
  bool throttleIncreasing;
  bool throttleDecreasing;
  Vector3 initialPosition;
  int jump_grace;
  bool jumping;
  bool bouncing;

  Default {
    +NOGRAVITY;
  }

  States {
    Spawn:
      Goto Ready;
      stop;
    Ready:
      SKYA A 3;
      SKYA B 3;
      SKYA C 3;
      SKYA D 3;
      loop;
    Rising:
      SKYJ A 3;
      SKYJ B 3;
      SKYJ C 3;
      SKYJ D 3;
      loop;
    Falling:
      SKYF A 3;
      SKYF B 3;
      SKYF C 3;
      SKYF D 3;
      loop;
    Kaboom:
      SBOO A 4 A_PlayWeaponSound("SBOOM");
      SBOO B 4;
      SBOO C 4;
      SBOO D 4;
      SBOO E 4;
      SBOO F 4;
      SBOO G 4;
      TNT1 A 20;
      TNT1 A 0 {
        Actor ship = Spawn("Ship", self.initialPosition);
        self.Destroy();
      }
      stop;
  }

  override void PostBeginPlay() {
    initialPosition = self.pos;
    movement = (0,0);
    throttle = 0.0;
    throttleDelta = 0.0;
    bouncing = false;
    jumping = false;
    forwardAngle = angle;
    movingLeft = false;
    movingRight = false;
    throttleIncreasing = false;
    throttleDecreasing = false;
  }

  void mLeft(bool a) {
    movingLeft = a;
  }
  void mRight(bool a) {
    movingRight = a;
  }
  void mJump(bool a) {
    jumping = a;
  }
  void throttleInc(bool a) {
    throttleIncreasing = a;
    setThrottleDelta();
  }
  void throttleDec(bool a) {
    throttleDecreasing = a;
    setThrottleDelta();
  }

  void setMovement() {
    float xspd = 0;
    if (movingLeft) {
      xspd -= throttle / 20 + 0.5;
    }
    if (movingRight) {
      xspd += throttle / 20 + 0.5;
    }
    movement = (xspd, movement.y);
  }

  void setThrottleDelta() {
    float throttle = 0;
    if (throttleIncreasing) {
      throttle += 4;
    }
    if (throttleDecreasing) {
      throttle -= 4;
    }
    throttleDelta = throttle;
  }

  void jump() {
    jump_grace = 0;
    bouncing = false;
    movement = (movement.x, -11.0);
    SetState(ResolveState("Rising"));
  }

  void bounce() {
    if (InStateSequence(CurState, ResolveState("Falling"))) {
      jump_grace = 0;
      bouncing = true;
      movement = (movement.x, -6.0);
      SetState(ResolveState("Rising"));
    }
  }
  
  void kaboom() {
    if (!InStateSequence(CurState, ResolveState("Kaboom"))) {
      SetState(ResolveState("Kaboom"));
    }
  }

  override void Tick() {
    Super.Tick();

    // can always alter throttle, even when jumping
    throttle = throttle + throttleDelta;
    if (throttle > 100) {
      throttle = 100;
    }
    if (throttle < 0) {
      throttle = 0;
    }


    if (InStateSequence(CurState, ResolveState("Ready"))) {
      jump_grace = 0;
    } else {
      jump_grace = jump_grace + 1;
      if (jump_grace > 1000) { // not sure if overflow; better be safe
        jump_grace = 1000;
      }
    }

    if (jump_grace < 5) {
      setMovement();
    }
    if (jumping) {
      if (jump_grace < 5 && bouncing == true || InStateSequence(CurState, ResolveState("Ready"))) {
        jump();
      }
    }
    // Console.Printf("Throttle: %f, ThrottleDelta: %f", throttle, throttleDelta);

    if (InStateSequence(CurState, ResolveState("Rising")) || InStateSequence(CurState, ResolveState("Falling"))) {
      movement = (movement.x, movement.y + 1.6);
    }

    bool hitFront = pos.z - self.getZAt(20, 0) < -10;
    bool hitLeft = pos.z - self.getZAt(0, -20) < -10;
    bool hitRight = pos.z - self.getZAt(0, 20) < -10;

    float hitz = self.GetZAt(0, 0);
    hitz = max(hitz, self.GetZAt(-20, 0));
    hitz = max(hitz, self.GetZAt(20, 0));
    hitz = max(hitz, self.GetZAt(0, -20));
    hitz = max(hitz, self.GetZAt(0, 20));

    float zoffset = pos.z - hitz; //pos.z - RemoteRay.HitLocation.z;

    if (hitFront) {
      throttle = 0;
    }

    if (hitLeft) {
      movement = (max(0, movement.x), 0);
    }

    if (hitRight) {
      movement = (min(0, movement.x), 0);
    }

    if (!InStateSequence(CurState, ResolveState("Kaboom"))) {
      if (zoffset > -10 && zoffset < movement.y) {
        if (movement.y > 7) {
          SetOrigin((pos.x, pos.y, hitz), false); // reset to floor position first
          bounce();
        } else {
          movement = (movement.x, 0);
          SetOrigin((pos.x, pos.y, hitz), false);
          SetState(ResolveState("Ready"));
          setMovement();
        }
      } else if (zoffset > 0.1) {
        if (movement.y < 0) {
          SetState(ResolveState("Rising"));
        } else {
          SetState(ResolveState("Falling"));
        }
      }
    }

    if (self.pos.z < -0.1) {
      self.kaboom();
    }

    SetOrigin((pos.x + (Sin(forwardAngle) * movement.x) + (Cos(forwardAngle) * throttle / 10), pos.y - (Cos(forwardAngle) * movement.x) + (Sin(forwardAngle) * throttle / 10), pos.z - movement.y), false);

    PlayerInfo p = players[consoleplayer];
    p.mo.SetOrigin((pos.x - (Cos(forwardAngle) * 200), pos.y - (Sin(forwardAngle) * 200), 40), true);
  }
}